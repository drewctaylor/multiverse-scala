story "Melrose Place" 10

state {

    ~ Amanda Woodward ~

    character amanda {
      life := "alive"
      first := "Amanda"
      last := "Woodward"
      gender := female
      orientation := male
      age := 30
      father := palmer
    }

    ~ Palmer Woodward, Amanda's Father ~

    character palmer {
      life := "alive"
      first := "Palmer"
      last := "Woodward"
      gender := male
      orientation := "female"
      age := 60
    }

    palmer->amanda {
      parent := true
    }

    amanda->palmer {
      child := true
    }

    amanda<->billy {
      marriage := true
      child := alison
    }

    ~ Alison Parker ~

    character alison {
        life := "alive"
        first := "Alison"
        last := "Parker"
        gender := female
        orientation := male
        age := 30
    }

    ~ Jane Andrews ~

    character jane {
        life := "alive"
        first := "Jane"
        last := "Andrews"
        gender := female
        orientation := male
        age := 30
    }

    ~ Jake Hanson ~

    character jake {
        life := "alive"
        first := "Jake"
        last := "Hanson"
        gender := male
        orientation := male union female
        age := 30
    }

    ~ Billy Campbell ~

    character billy {
        life := "alive"
        first := "Billy"
        last := "Campbell"
        gender := male
        orientation := female
        age := 30
    }

    ~ Matt Fielding ~

    character matt {
        life := "alive"
        first := "Matt"
        last := "Fielding"
        gender := male
        orientation := male
        age := 30
    }

    ~ Peter Burns ~

    character peter {
        life := "alive"
        first := "Peter"
        last := "Burns"
        gender := male
        orientation := female
        age := 30
    }

    ~ Michael Mancini ~

    character michael {
        life := "alive"
        first := "Michael"
        last := "Mancini"
        gender := male
        orientation := female
        age := 30
    }

    ~ Sydney Andrews ~

    character sydney {
        life := "alive"
        first := "Sydney"
        last := "Andrews"
        gender := female
        orientation := male union female
        age := 30
    }

    ~ Kimberly Shaw ~

    character kimberly {
        life := "alive"
        first := "Kimberly"
        last := "Shaw"
        gender := female
        orientation := male union female
        age := 30
    }

    ~ Jane and Sydney are sisters. ~

    jane<->sydney {
        family := true
    }
}

goal life (character c) "Character Life" {
    strategy "No Operation" {
        ?= c.life == "alive"

        "<p>{{c.first}} lies by the pool.</p>"
    }

    strategy "Resurrect" {
        ?= c.life == "dead"

        "<p>{{c.first}} returns to Melrose Place, very much alive.</p>"
    }
}

goal single (character c1) "Character Single" {
    strategy "No Operation" {
        character c2 ? (c1<->c2).marriage == empty

        "<p>{{c1.first}} lies by the pool, and {{c1.sub}} dreams of love while sunning {{c1.ref}}.</p>"
    }

    strategy "By Death" {
        character c2 ?= (c1<->c2).marriage != empty

        c2.life := "dead"

        (c1<->c2).marriage := empty

        "<p>{{c2.first}} dies, leaving {{c1.first}} alone.</p>"
    }

    strategy "By Divorce" {
        character c2 ?= (c1<->c2).marriage != empty

        (c1<->c2).marriage := empty

        "<p>{{c2.first}} and {{c1.first}} divorce.</p>"
    }
}

goal marry () "Marriage Scene" {
  strategy "Marriage Strategy" {
      character c1, character c2 ?= c1 != c2 &&
        c1.gender subset c2.orientation &&
        c2.gender subset c1.orientation &&
        (c1<->c2).marriage == empty

      life (c1)
      life (c2)
      single (c1)
      single (c2)

      (c1<->c2).marriage := true

      "<p>{{c1.first}} and {{c2.first}} marry.</p>"
    }
}

goal divorce () "Divorce Scene" {
    strategy "Divorce Strategy" {
        character c1, character c2 ?= (c1<->c2).marriage

        (c1<->c2).marriage := empty

        "<p>{{c1.first}} and {{c2.first}} divorce.</p>"
    }
}

goal forcedMarriage() "Forced Marriage Plot" {
  strategy {
    character child, character parent ?=
      (child->parent).child &&
      (parent->child).parent &&
      parent.life == "alive"

    character spouse ?= (child<->spouse).marriage

    character lover ?=
      child  != lover &&
      parent != lover &&
      spouse != lover &&
      child.gender subset lover.orientation

    "<p>
      Although {{child.first}} and {{spouse.first}} are happily married,
      {{parent.first}} does not approve of {{spouse.first}}, and {{parent.sub}}
      would rather that {{child.first}} marry {{parent.pos}} protoge,
      {{lover.first}}. One day, {{parent.first}} pulls {{child.first}} aside and
      tells {{child.obj}}, \"Divorce {{spouse.first}}, or I'll ruin {{spouse.obj}}.\"
    </p>"

    (child<->spouse).marriage := empty

    "<p>
      {{child.first}} steels {{child.ref}}, then confronts {{spouse.first}}: 'I
      want a divorce.'
    </p>"

    (child<->lover).marriage := true

    "<p>
      Soon after, {{child.first}} marries {{lover.first}}.
    </p>"

    character friend ?=
      friend != lover &&
      friend != child &&
      friend != spouse &&
      friend != parent

    "<p>
      Shocked, {{spouse.first}} is comforted by {{spouse.det}} friend, {{friend.first}}.
    </p>"

    character rebound ?=
      rebound != lover &&
      rebound != child &&
      rebound != spouse &&
      rebound != parent &&
      spouse.gender subset rebound.orientation &&
      rebound.gender subset spouse.orientation

    "<p>
      Ultimately, {{spouse.first}} finds a new love in {{rebound.first}}, and they marry.
    </p>"

    (spouse<->rebound).marriage := true

    parent.life := "dead"
    (child<->lover).marriage := false

    "<p>
      {{parent.first}} unexpectedly dies, leaving {{child.first}} free to divorce {{child.det}} spouse.
    </p>"
  }
}
